# $Id: Makemf.config,v 2002.12 2003/06/25 23:09:03 asgulle Exp $

set targets = (fortdriver cxxdriver make_unit_test)

source ${SCBS_dir}/prepare_makefiles.csh || exit 1


##########################################################################

if (! $?OPT_nompi) then

  # convert make_utils arch type into Zoltan ARCH_TYPE
  switch ($PLT_sys_type)
    
    case "sun":
      if ("$PLT_compilers" == "sun4") then
        set zoltan_arch = "sunos_ws4.2"
      else if ("$PLT_compilers" == "sun5") then
        set zoltan_arch = "sunos_ws5.0"
      else if ("$PLT_compilers" == "sun6") then
        set zoltan_arch = "sunos_ws6.0"
      else if ("$PLT_compilers" == "forte7") then
        set zoltan_arch = "sunos_forte7"
      else if ("$PLT_compilers" == "xtflops") then
        set zoltan_arch = "xtflops"
      else if ($?OPT_gnu) then
        set zoltan_arch = "gcc"
      else
        set zoltan_arch = "solaris"
      endif
    breaksw
    
    case "osf1":
      set zoltan_arch = "osf1"
      if ("$PLT_compilers" == "cplant") set zoltan_arch = "cplant"
    breaksw
    
    case "irix":
      set zoltan_arch = "irix_64"
      if ("$PLT_compilers" == "32bit") set zoltan_arch = "irix_32"
    breaksw
    
    case "linux":
      set zoltan_arch = "linux"
      if ("$PLT_compilers" == "cplant") set zoltan_arch = "ross"
    breaksw
    
    case "darwin":
      set zoltan_arch = "darwin"
    breaksw
    
    case "hp-ux":
      set zoltan_arch = "hp"
    breaksw
    
    case "aix":
      set zoltan_arch = "ibm"
      if ("$PLT_compilers" == "aix_white") set zoltan_arch = "ibm.white"
    breaksw

    default:
      echo 'contact Makemf:  ERROR: architecture type conversion unknown for "'$PLT_sys_type'"'
      exit 1
    breaksw
    
  endsw

endif

##########################################################################

set myincl     = "INCL = -I../search -I../enforcement"
set mylibs     = "LIBS = -L../ -lacme"

if (! $?OPT_noexo) then
  set myincl = "$myincl"' $(NEMESIS_INCL)'
  set mylibs = "$mylibs"' $(NEMESIS_LIBS)'
endif

if (! $?OPT_nompi) then
  set myincl = "$myincl"' $(MPI_INCL) -I../Zoltan/include'
  set mylibs = "$mylibs -L../Zoltan/Obj_$zoltan_arch -lzoltan"' $(MPI_LIBS)'
endif

# ------------------------------------------------------------- #
#  fortdriver target:

(cat >> $fortdriver_Mf) << 'EOF'

SRCS = fortdriver.F \
       read_mesh.C \
       read_data.C \
       test_init.C \
       restart.C \
       modify_topology.C \
       new_dlb.C \
       output_interactions.C

LINKER   = $(LINK.f)

build_all: depend compile link
	$(MAKE) T=cxxdriver
	$(MAKE) T=make_unit_test

'EOF'

echo "$myincl"                              >> $fortdriver_Mf
if ($?OPT_nompi) then
echo "$mylibs"' $(FLD_CCLIBS)'              >> $fortdriver_Mf
else
echo "$mylibs"' $(MPI_FLIBS) $(FLD_CCLIBS)' >> $fortdriver_Mf
endif


# ------------------------------------------------------------- #
#  cxxdriver target:

(cat >> $cxxdriver_Mf) << 'EOF'

SRCS = cxxdriver.C \
       read_mesh.C \
       read_data.C \
       test_init.C \
       restart.C \
       modify_topology.C \
       new_dlb.C \
       output_interactions.C

LINKER   = $(LINK.C)

build_all: depend compile link
	@echo " "

'EOF'

echo "$myincl"                 >> $cxxdriver_Mf
echo "$mylibs"' $(CCLD_FLIBS)' >> $cxxdriver_Mf


# ------------------------------------------------------------- #
if (! $?OPT_noexo) then

#  make_unit_test target (only make makefile if exodus is active):

(cat >> $make_unit_test_Mf) << 'EOF'

SRCS = make_unit_test.C

LINKER   = $(LINK.C)

build_all: depend compile link
	@echo " "

'EOF'

echo "$myincl"                 >> $make_unit_test_Mf
echo "$mylibs"' $(CCLD_FLIBS)' >> $make_unit_test_Mf

endif

##########################################################################
